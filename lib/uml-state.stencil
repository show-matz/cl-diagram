
(in-package :cl-diagram-user)

(load-stencil :uml-keyword-info)
(load-stencil :uml-transition-spec)

(defparameter *uml-state-font*         nil)
(defparameter *uml-state-fill*      :white)
(defparameter *uml-state-stroke*    :black)
(defparameter *uml-state-width*         80)
(defparameter *uml-state-height*        40)
(defparameter *uml-state-corner-r*      10)
(defparameter *uml-state-margin*         5)

;-------------------------------------------------------------------------------
;
; class uml-state
;
;-------------------------------------------------------------------------------
(defclass uml-state (diagram:text-shape)
  ((keyword		:initform nil :initarg :keyword)	; (or nil uml-keyword-info)
   (contents-p	:initform nil :initarg :contents-p)	; boolean
   (activities	:initform nil :initarg :activities)	; list of uml-transition-spec
   (corner-r	:initform   0 :initarg :corner-r)	; number
   (margin2		:initform   0 :initarg :margin)))	; number ( margin for keyword )

(defmethod initialize-instance :after ((state uml-state) &rest initargs)
  (declare (ignore initargs))
  (with-slots (keyword activities margin2 contents-p) state
	(setf keyword (and keyword (make-uml-keyword keyword)))
	(setf activities (mapcar (lambda (entry)
							   (make-uml-transition-spec entry)) activities))
	(setf contents-p (not (not (or activities contents-p))))
	(setf margin2 (if (null keyword)
					  0
					  (or (and *uml-state-font*  (slot-value *uml-state-font* 'diagram::size))
						  (and *default-font*    (slot-value *default-font*   'diagram::size))
						  12))))	;;ToDo : これはなんとかしないと。
  state)

(defmethod check ((state uml-state) canvas dict)
  ;; this method must call super class' one.
  (call-next-method)
  (with-slots (keyword activities corner-r margin2) state
	(check-object keyword    canvas dict :nullable   t :class uml-keyword-info)
	(check-member activities :nullable   t :types list)
	(check-member corner-r   :nullable nil :types number)
	(check-member margin2    :nullable nil :types number)
	(dolist (activity activities)
	  (check-object activity canvas dict :class uml-transition-spec)))
  nil)

;; no override
;;(defmethod shape-connect-point ((state uml-state) type1 type2 arg)
;;  (call-next-method))

;;MEMO : use impelementation of group...
;;(defmethod group-get-canvas ((state uml-state)) ...)

(defmethod shape-get-subcanvas ((state uml-state))
  (with-slots (keyword
			   (text diagram::text)
			   (font diagram::font)
			   (margin diagram::margin)) state
	(multiple-value-bind (width height)	;; start by w/h of 'text'.
		(font-calc-textarea font
							(if (stringp text)
								text
								(string-downcase (symbol-name text))))
	  (declare (ignore width))
	  (incf height (* margin 2))
	  ;; keyword (if exists).
	  (when keyword
		(with-slots (font) keyword
		  (multiple-value-bind (w h)
			  (font-calc-textarea font (uml-keyword-to-string keyword))
			(declare (ignore w))
			(incf height h))))
	  (make-canvas (+ (shape-top state) height)
				   (shape-bottom state)
				   (shape-left   state)
				   (shape-right  state)))))

;; override of group::draw-group
(defmethod draw-group ((state uml-state) writer)
  (let ((canvas (group-get-canvas state)))
	(let ((top    (canvas-top    canvas))
		  (left   (canvas-left   canvas))
		  (width  (canvas-width  canvas))
		  (height (canvas-height canvas)))
	  (macrolet ((register-entity (entity)
				   `(check-and-draw-local-entity ,entity canvas writer)))
		(with-slots (keyword margin2 corner-r activities contents-p
							 diagram::margin diagram::fill diagram::stroke) state
		  ;; draw state body
		  (rectangle (/ width 2) (/ height 2)
					 width height :rx corner-r :ry corner-r
					 :fill diagram::fill :stroke diagram::stroke)
		  ;; draw keyword
		  (when keyword
			(uml-keyword-draw keyword
							  (+ left (/ width 2))
							  (+ top  diagram::margin margin2) :center writer))
		  ;; draw text
		  (call-next-method)
		  ;;contents
		  (when contents-p
			(let ((y (+ margin2
						(canvas-height (text-shape-paragraph-area state)))))
			  ;;draw partition line
			  (line `(0 ,y ,width ,y))
			  ;;ToDo : 0UB9rp9CxaC : 以下の incf は必要なはずだが、あると少しズレてしまう．．．（汗
			  ;;(incf y diagram::margin)
			  ;;activities がある場合は順番に描画する必要がある
			  (dolist (act activities)
				(when act
				  (let ((txt (uml-transition-spec-to-string act)))
					(with-slots (font) act
					  (with-slots ((fsize diagram::size)
								   (ls diagram::line-spacing)) font
						(incf y (+ ls fsize))
						(text diagram::margin y txt :align :left))))))))))))
  nil)


;for debug...
;;(defmethod post-draw ((state uml-state) writer)
;;  (call-next-method)
;;  (with-slots (contents-p) state
;;	(when contents-p
;;	  (draw-canvas-frame (shape-get-subcanvas state) writer))))
 
(defmethod text-shape-calc-size ((state uml-state))
  (multiple-value-bind (width height) (call-next-method)	;; text area ( includes margin ).
	(with-slots (keyword activities contents-p
						 (margin diagram::margin)) state
	  ;; keyword (if exists).
	  (when keyword
		(with-slots (font) keyword
		  (multiple-value-bind (w h)
				(font-calc-textarea font (uml-keyword-to-string keyword))
			(setf width (cl:max width (+ w (* margin 2))))
			(incf height h))))
	  ;; contents
	  (when contents-p
		(incf height (* margin 2))
		(dolist (act activities)
		  (let ((txt (uml-transition-spec-to-string act)))
			(with-slots (font) act
			  (with-slots ((ls diagram::line-spacing)) font
				(multiple-value-bind (w h) (font-calc-textarea font txt)
				  (setf width (cl:max width (+ w (* margin 2))))
				  (incf height (+ h ls)))))))))
	(values (cl:max width  *uml-state-width*)
			(cl:max height *uml-state-height*))))


(defmethod text-shape-paragraph-area ((state uml-state))
  (let ((canvas (call-next-method)))
	(with-slots (text contents-p margin2
					  diagram::font diagram::margin) state
	  (incf (canvas-top canvas) margin2)
	  (when contents-p
		(multiple-value-bind (w h)
				(font-calc-textarea diagram::font text)
		  (declare (ignore w))
		  (setf (canvas-bottom canvas)
				(+ (canvas-top canvas) (* 2 diagram::margin) h)))))
	canvas))

(defmethod shape-connect-point ((state uml-state) type1 type2 arg)
  (rectangle-connect-point (shape-center state)
						   (shape-middle state)
						   (shape-width  state)
						   (shape-height state) type1 type2 arg))

;-------------------------------------------------------------------------------
;
; macro uml-state
;
;-------------------------------------------------------------------------------
(defmacro uml-state (x y text &key keyword width height
									activities margin corner-r
									font fill stroke link layer id contents)
  (let* ((contents-p (not (null contents)))
		 (code `(register-entity (make-instance 'uml-state
												:center-x ,x :center-y ,y
												:width ,width :height ,height
												:text ,text :keyword ,keyword
												:contents-p ,contents-p
												:corner-r (or ,corner-r *uml-state-corner-r*)
												:margin   (or ,margin   *uml-state-margin*)
												:activities ',activities :font ,font
												:fill     (or ,fill *uml-state-fill*
															  *default-fill*   :white)
												:stroke   (or ,stroke *uml-state-stroke*
															  *default-stroke* :black)
												:link ,link :layer ,layer :id ,id))))
	(if (null contents)
		code
		  (let ((g-obj (gensym "OBJ")))
			`(let* ((,g-obj ,code)
					(canvas (shape-get-subcanvas ,g-obj)))
			   (declare (special canvas))
			   ,@contents)))))

