
(in-package :cl-diagram-user)

(load-stencil :uml-keyword-info)

(defparameter *uml-action-font*         nil)
(defparameter *uml-action-fill*      :white)
(defparameter *uml-action-stroke*    :black)
(defparameter *uml-action-width*         80)
(defparameter *uml-action-height*        40)
(defparameter *uml-action-corner-r*       6)
(defparameter *uml-action-margin*         5)

;-------------------------------------------------------------------------------
;
; class uml-action
;
;-------------------------------------------------------------------------------
(defclass uml-action (diagram:text-shape)
  ((keyword		:initform nil	:initarg  :keyword)		; (or nil uml-keyword-info)
   (contents-p	:initform nil	:initarg  :contents-p)	; boolean
   (corner-r	:initform nil	:initarg  :corner-r)	; number
   (kwd-height	:initform nil)))						; number

(defmethod initialize-instance :after ((action uml-action) &rest initargs)
  (declare (ignore initargs))
  ;;(format t "initialize-instance :after called.~%")
  (with-slots (keyword kwd-height) action
	(setf keyword (and keyword (make-uml-keyword keyword)))
	(setf kwd-height (if (null keyword)
						 0
						 (or (and *uml-action-font* (slot-value *uml-action-font* 'diagram::size))
							 (and *default-font*    (slot-value *default-font*    'diagram::size))
							 12))))	;;ToDo : これはなんとかしないと。
  action)

(defmethod check ((action uml-action) canvas dict)
  ;;(format t "check called.~%")
  (with-slots (keyword corner-r kwd-height) action
	(check-object keyword  canvas dict :nullable t :class uml-keyword-info)
	(check-member corner-r :nullable nil :types number)
	(check-member kwd-height  :nullable nil :types number))
  ;; this method must call super class' one.
  (call-next-method))

;; no override
;;(defmethod shape-connect-point ((action uml-action) type1 type2 arg)
;;  (call-next-method))

;;MEMO : use impelementation of group...
;;(defmethod group-get-canvas ((action uml-action)) ...)

;; override of group::draw-group
(defmethod draw-group ((action uml-action) writer)
  ;;(format t "draw-group called.~%")
  (let ((canvas (group-get-canvas action)))
	(let ((top    (canvas-top    canvas))
		  (left   (canvas-left   canvas))
		  (width  (canvas-width  canvas))
		  (height (canvas-height canvas)))
	  (macrolet ((register-entity (entity)
				   `(check-and-draw-local-entity ,entity canvas writer)))
		(with-slots (keyword kwd-height corner-r
						 diagram::margin diagram::fill diagram::stroke) action
		  ;; draw action body
		  (rectangle (/ width 2) (/ height 2)
					 width height :rx corner-r :ry corner-r
					 :fill diagram::fill :stroke diagram::stroke)
		  ;; draw keyword
		  (when keyword
			(uml-keyword-draw keyword
							  (+ left (/ width 2))
							  (+ top  diagram::margin kwd-height) :center writer))
		  ;; draw text
		  (call-next-method)))))
  nil)


;for debug...
;(defmethod post-draw ((action uml-action) writer)
;  ;;(format t "post-draw called.~%")
;  (call-next-method)
;  (with-slots (contents-p) action
;	(when contents-p
;	  (draw-canvas-frame (shape-get-subcanvas action) writer))))
 

(defmethod text-shape-calc-size ((action uml-action))
  ;;(format t "text-shape-calc-size called.~%")
  (multiple-value-bind (width height) (call-next-method)
	(with-slots (keyword contents-p
						 (margin diagram::margin)) action
	  ;; ToDo : margin は上記 call-next-method の結果に含まれているはずだが、何故か必要。
	  (incf height (* margin 2))
	  ;; keyword (if exists).
	  (when keyword
		(with-slots (font) keyword
		  (multiple-value-bind (w h)
				(font-calc-textarea font (uml-keyword-to-string keyword))
			(setf width (cl:max width (+ w (* margin 2))))
			(incf height h)))))
	(values (cl:max width  *uml-action-width*)
			(cl:max height *uml-action-height*))))

(defmethod text-shape-paragraph-area ((action uml-action))
  ;;(format t "text-shape-paragraph-area called.~%")
  (let ((canvas (call-next-method)))
	(with-slots (text contents-p kwd-height
					  diagram::font diagram::margin) action
	  (incf (canvas-top canvas) (/ kwd-height 2))
	  (when contents-p
		(multiple-value-bind (w h)
				(font-calc-textarea diagram::font text)
		  (declare (ignore w))
		  (setf (canvas-bottom canvas)
				(+ (canvas-top canvas) kwd-height (* 2 diagram::margin) h)))))
	canvas))

(defmethod shape-get-subcanvas ((action uml-action))
  ;;(format t "shape-get-subcanvas called.~%")
  (with-slots (diagram::margin) action
	(let ((partition (+ diagram::margin
						(canvas-height (text-shape-paragraph-area action)))))
	  (make-canvas (+ (shape-top action) partition)
				   (shape-bottom action)
				   (shape-left   action)
				   (shape-right  action)))))

(defmethod shape-connect-point ((action uml-action) type1 type2 arg)
  (rectangle-connect-point (shape-center action)
						   (shape-middle action)
						   (shape-width  action)
						   (shape-height action) type1 type2 arg))
  
;-------------------------------------------------------------------------------
;
; macro uml-action
;
;-------------------------------------------------------------------------------
(defmacro uml-action (x y text &key keyword width height
									margin corner-r
									font fill stroke link layer id contents)
  (let* ((contents-p (not (null contents)))
		 (code `(register-entity
					 (make-instance 'uml-action
									:center-x ,x :center-y ,y
									:width ,width :height ,height
									:text ,text :keyword ,keyword
									:contents-p ,contents-p
									:corner-r (or ,corner-r *uml-action-corner-r*)
									:margin   (or ,margin   *uml-action-margin*)
									:font     ,font 
									:fill     (or ,fill *uml-action-fill*
												  *default-fill*   :white)
									:stroke   (or ,stroke *uml-action-stroke*
												  *default-stroke* :black)
									:link ,link :layer ,layer :id ,id))))
	(if (null contents)
		code
		(let ((g-obj (gensym "OBJ")))
		  `(let* ((,g-obj ,code)
				  (canvas (shape-get-subcanvas ,g-obj)))
			 (declare (special canvas))
			 ,@contents)))))

