
(in-package :cl-diagram-user)

(load-stencil :uml-keyword-info)

(defparameter *uml-implementation-dasharray* '(4 2))

(defclass uml-implementation (diagram:connector)
  ((keyword	;:type     (or nil uml-keyword-info)
			:initform nil
			:initarg  :keyword
			:accessor uml-implementation-keyword)))

(defmethod initialize-instance :after ((gen uml-implementation) &rest initargs)
  (declare (ignore initargs))
  (with-slots (keyword) gen
	(setf keyword (and keyword (make-uml-keyword keyword))))
  gen)


(defmethod check ((gen uml-implementation) canvas dict)
  ;; this method must call super class' one.
  (call-next-method)
  (with-slots (keyword) gen 
	(check-object keyword canvas dict :nullable t :class uml-keyword-info))
  nil)

(defmethod entity-composition-p ((gen uml-implementation))
  (with-slots (keyword) gen
	(or keyword
		(call-next-method))))

(defmethod post-draw ((gen uml-implementation) writer)
  (with-slots (keyword) gen
	(when keyword
	  (uml-keyword-draw-beside-line keyword gen writer)))
  (call-next-method))
	  


(defmacro uml-implementation (from to &key keyword style layer id)
  `(register-entity (make-instance 'uml-implementation
								   :from ,from :to ,to
								   :keyword ,keyword :style ,style
								   :class nil
								   :stroke (make-stroke
											:dasharray (or *uml-implementation-dasharray* '(4 2))
											:base *default-stroke*)
								   :end1 nil :end2 `(:type :triangle :fill :white
													 :stroke (:dasharray nil
															  :base ,*default-stroke*))
								   :layer ,layer :id ,id)))

