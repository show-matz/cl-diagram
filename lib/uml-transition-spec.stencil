(in-package :cl-diagram-user)

(defparameter *uml-transition-spec-font*  nil)
(defparameter *uml-transition-spec-class* nil)

;-------------------------------------------------------------------------------
;
; class uml-transition-spec
;
;-------------------------------------------------------------------------------
(defclass uml-transition-spec ()
  ((trigger	:initform nil :initarg :trigger)	; (or keyword string)
   (guard	:initform nil :initarg :guard)		; (or keyword string)
   (action	:initform nil :initarg :action)		; (or keyword string)
   (class	:initform nil :initarg :class)		; (or keyword string)
   (font	:initform nil :initarg :font)))		; (or nil font-info)


;;MEMO : no impl.
;(defmethod initialize-instance :after ((obj uml-transition-spec) &rest initargs)
;;  ...)

(defmethod check ((mul uml-transition-spec) canvas dict)
  (with-slots (trigger guard action class font) mul
	(check-member trigger :nullable t :types (or keyword string))
	(check-member guard   :nullable t :types (or keyword string))
	(check-member action  :nullable t :types (or keyword string))
	(check-member class   :nullable t :types (or keyword string))
	(check-object font    canvas dict :nullable t :class font-info))
  nil)

(defun uml-transition-spec-to-string (obj)
  (with-slots (trigger guard action) obj
	(format-string trigger
				   (when guard
					 (format-string "[" guard "]"))
				   (when action
					 (format-string "/" action)))))

(defun uml-transition-spec-draw (obj x y align writer)
  (let ((txt (uml-transition-spec-to-string obj))
		(txt-anchor (ecase align
					  ((:left)   "start")
					  ((:center) "middle")
					  ((:right)  "end"))))
	(with-slots (class font) obj
	  (writer-write writer
					"<text "
					"x='" x "' "
					"y='" y "' "
					"text-anchor='" txt-anchor "' "
					(write-when class "class='" it "' ")
					(when font
					  (to-property-strings font))
					">" (escape-characters txt) "</text>"))))

(defun uml-transition-spec-draw-beside-line (obj line writer)
  (unless (typep line 'diagram:line)
	(throw-exception "multiplicity-info : line parameter is not line object"))
  (multiple-value-bind (x y sin cos) (diagram::line-get-center line)	;;ToDo : export!?
	(declare (ignorable x y sin cos))
	(let* ((f-size (slot-value (slot-value obj 'font) 'diagram::size))
		   (offset f-size)
		   (align (cond ((zerop sin) :center)
						((<  0  sin) :left)
						(t           :right))))
	  (incf y (/ f-size 2))
	  (uml-transition-spec-draw obj (+ x (* sin offset))
									 (- y (* cos offset)) align writer))))


(defun make-uml-transition-spec (&rest params)
  (if (= 1 (length params))
	  (let ((param (car params)))
		(cond
		  ((typep   param  'uml-transition-spec) param)
		  ((listp   param) (apply #'make-uml-transition-spec param))
		  (t               (make-uml-transition-spec :trigger param))))
	  (destructuring-bind (&key trigger guard action
								(class nil class-p)
								(font  nil  font-p)) params
		(make-instance 'uml-transition-spec
					   :trigger   trigger
					   :guard     guard
					   :action    action
					   :class    (if class-p class *uml-transition-spec-class*)
					   :font     (if font-p  font  (or *uml-transition-spec-font*
															diagram:*default-font*))))))

