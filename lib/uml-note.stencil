
(in-package :cl-diagram-user)

(load-stencil :uml-keyword-info)

(defparameter *uml-note-font*         nil)
(defparameter *uml-note-fill*      :white)
(defparameter *uml-note-stroke*       nil)
(defparameter *uml-note-margin*        10)
(defparameter *uml-note-align*      :left)
(defparameter *uml-note-valign*   :center)
(defparameter *uml-note-crease-size*   20)

(defclass uml-note (diagram:text-shape)
  ((keyword	;:type     (or nil uml-keyword-info)
			:initform nil
			:initarg  :keyword
			:accessor uml-note-keyword)
   (targets	;:type     list of keyword symbol
			:initform nil
			:initarg  :targets
			:accessor uml-note-targets)
   (margin2	;:type     number    ; margin for keyword
			:initform nil
			:initarg  :margin
			:accessor uml-note-margin2)
   (crease	;:type     number
			:initform nil
			:initarg  :margin
			:accessor uml-note-crease)))


(defmethod initialize-instance :after ((note uml-note) &rest initargs)
  (declare (ignore initargs))
  (with-slots (keyword targets margin2 crease) note
	(setf keyword (and keyword (make-uml-keyword keyword)))
	(setf targets (if (listp targets) targets (list targets)))
	(setf margin2 (if (null keyword)
					  0
					  (or (and *uml-note-font* (diagram::font-size *uml-note-font*))
						  (and *default-font*  (diagram::font-size *default-font*))
						  12)))	;;ToDo : これはなんとかしないと。
	(setf crease  (or crease *uml-note-crease-size*)))
  note)

(defmethod check ((note uml-note) canvas dict)
  (check-object (keyword (uml-note-keyword note)) canvas dict :nullable t :class uml-keyword-info)
  (check-member (targets (uml-note-targets note)) :nullable   t :types list)
  (dolist (id (uml-note-targets note))
	(check-member (target id) :nullable nil :types keyword))
  (check-member (margin2 (uml-note-margin2 note)) :nullable nil :types number)
  (check-member (crease  (uml-note-crease  note)) :nullable nil :types number)
  (with-slots (targets) note
	(setf targets (mapcar (lambda (id)
							(let ((ent (diagram::dict-get-entity dict id)))
							  (unless ent
								(throw-exception "ERROR : entity '~A' not found in dictionary." id))
							  ent)) targets)))
  ;; this method must call super class' one.
  (call-next-method))


;; override of group::draw-group
(defmethod draw-group ((note uml-note) writer)
  (let ((canvas (group-get-canvas note)))
	(let ((top    (canvas-top    canvas))
		  (left   (canvas-left   canvas))
		  (width  (canvas-width  canvas))
		  (height (canvas-height canvas)))
	  (macrolet ((register-entity (entity)
				   `(check-and-draw-local-entity ,entity canvas writer)))
		(with-slots (keyword margin2 crease
							 diagram::fill diagram::stroke diagram::margin) note
		  ;; draw note body
		  (polygon `(0 0 0 ,height ,width ,height
					   ,width ,crease ,(- width crease) 0)
				   :fill diagram::fill :stroke diagram::stroke)
		  ;; draw keyword
		  (when keyword
			(uml-keyword-draw keyword (+ left diagram::margin)
							  (+ top (/ diagram::margin 2) margin2) :left writer))
		  ;; draw text
		  (call-next-method)
		  ;; draw crease
		  (polygon `(,width ,crease ,(- width crease) ,crease ,(- width crease) 0)
				   :fill diagram::fill :stroke diagram::stroke)))))
  nil)

;for debug...
;(defmethod post-draw ((obj uml-note) writer)
;  (call-next-method)
;  (draw-canvas-frame (shape-get-subcanvas obj) writer))
 

(defun draw-connection (note writer)
  (let* ((canvas (group-get-canvas note))
		 (width  (canvas-width  canvas))
		 (height (canvas-height canvas)))
	(macrolet ((register-entity (entity)
				 `(check-and-draw-local-entity ,entity canvas writer)))
	  (with-slots (targets diagram::stroke) note
		;; draw connections
		(let ((canvas (make-canvas 0 height 0 width))
			  (*default-stroke* (make-stroke :dasharray '(3 3)
											 :base diagram::stroke))) ;;ToDo : fixed ?
		  (dolist (ent targets)
			(line (resolve-connector-points note ent :CC)))))))
  nil)

(defmethod draw-entity ((note uml-note) writer)
  (let ((result (call-next-method)))
	(draw-connection note writer)
	result))

(defmethod text-shape-calc-size ((note uml-note))
  (multiple-value-bind (w h) (call-next-method)
	(values w (+ h (uml-note-margin2 note)))))

(defmethod text-shape-paragraph-area ((note uml-note))
  (let ((canvas (call-next-method)))
	(incf (canvas-top canvas)
		  (uml-note-margin2 note))
	canvas))


(defmacro uml-note (x y text &key keyword targets width height
							   align valign font fill stroke margin link layer id)
  `(register-entity (make-instance 'uml-note
								   :center-x ,x :center-y ,y
								   :width ,width :height ,height
								   :text ,text :font ,font
								   :keyword ,keyword :targets ,targets
								   :align  (or ,align  *uml-note-align*)
								   :valign (or ,valign *uml-note-valign*)
								   :margin (or ,margin *uml-note-margin*)
								   :fill ,fill :stroke ,stroke
								   :link ,link :layer ,layer :id ,id)))

